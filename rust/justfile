arch := "arm-unknown-linux-gnueabihf"
artifacts_dir := "../artifacts/"

build-all: blinky-build blinky-bm-build motor-build motor-bm-build pid-build pid-bm-build dmc-build

watch-all:
  just blinky-watch \
  & just blinky-bm-watch \
  & just motor-watch \
  & just motor-bm-watch \
  & just pid-watch \
  & just pid-bm-watch \
  & just dmc-watch \
  & wait

clean:
  rm -rf ./target

blinky-build: (_build "1" "blinky")
blinky-watch: (_watch "1" "blinky")

blinky-bm-build: (_build-bm "1" "blinky-bm")
blinky-bm-watch: (_watch-bm "1" "blinky-bm")
blinky-bm-flash: (_run-bm "1" "blinky-bm")

motor-build: (_build "2" "motor")
motor-watch: (_watch "2" "motor")

motor-bm-build: (_build-bm "2" "motor-bm")
motor-bm-watch: (_watch-bm "2" "motor-bm")
motor-bm-flash: (_run-bm "2" "motor-bm")

pid-build: (_build "3" "pid")
pid-watch: (_watch "3" "pid")

pid-bm-build: (_build-bm "3" "pid-bm")
pid-bm-watch: (_watch-bm "3" "pid-bm")
pid-bm-flash: (_run-bm "3" "pid-bm")

dmc-build: (_build "4" "dmc")
dmc-watch: (_watch "4" "dmc")

_watch NUM PACKAGE:
  watchexec --postpone --wrap-process session --verbose \
  -e rs,toml -w ./{{NUM}}-{{PACKAGE}} \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build \"{{NUM}}\" \"{{PACKAGE}}\"

_build NUM PACKAGE:
  cross build --package {{PACKAGE}} --release --target={{arch}} \
  && cp ./target/{{arch}}/release/{{PACKAGE}} ../artifacts/{{NUM}}-{{PACKAGE}}-rust

_watch-bm NUM PACKAGE:
  watchexec --postpone --wrap-process session --verbose \
  -e rs,toml -w ./{{NUM}}-{{PACKAGE}} \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build-bm \"{{NUM}}\" \"{{PACKAGE}}\"

_build-bm NUM PACKAGE:
  cd ./{{NUM}}-{{PACKAGE}}/ && cargo build --release

_run-bm NUM PACKAGE:
  cd ./{{NUM}}-{{PACKAGE}}/ && cargo run --release
