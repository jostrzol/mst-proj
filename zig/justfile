release := ""
#release := "fast"

arch := "arm-linux-gnueabihf"
cpu := "arm1176jzf_s"
artifacts_dir := "../artifacts/"

build-all: blinky-build blinky-bm-build motor-build pid-build

watch-all:
  just blinky-watch \
  & just blinky-bm-watch \
  & just motor-watch \
  & just pid-watch \
  & wait

clean:
  rm -rf ./*/{.zig-cache,zig-out}

blinky-build: (_build "1-blinky")
blinky-watch: (_watch "1-blinky")

blinky-bm-build: (_build-bm "1-blinky-bm")
blinky-bm-watch: (_watch-bm "1-blinky-bm")
blinky-bm-flash: (_cmd-bm "1-blinky-bm" "flash" "monitor")
blinky-bm-cmd *CMD: (_cmd-bm "1-blinky-bm" CMD)

motor-build: (_build "2-motor")
motor-watch: (_watch "2-motor")

pid-build: (_build "3-pid")
pid-watch: (_watch "3-pid")

_watch PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{PROJECT}} \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build \"{{PROJECT}}\"

_build PROJECT:
  cd {{PROJECT}} \
  && zvm run 0.13.0 build -- \
    -Dtarget={{arch}} \
    -Dcpu={{cpu}} \
    {{ if release != "" {"--release="+release} else {""} }} \
    -freference-trace \
  && cp ./zig-out/bin/* ../../artifacts/

_watch-bm PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{PROJECT}} \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build-bm \"{{PROJECT}}\"

_build-bm PROJECT: (_cmd-bm PROJECT "build")
  cd {{PROJECT}} && idf.py build

_cmd-bm PROJECT *CMD:
  cd {{PROJECT}} && idf.py {{CMD}}
