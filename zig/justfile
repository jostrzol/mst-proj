# release := ""
release := "small"

arch := "arm-linux-gnueabihf"
cpu := "arm1176jzf_s"
artifacts_dir := "../artifacts/"

build-all: blinky-build blinky-bm-build motor-build motor-bm-build pid-build pid-bm-watch

watch-all:
  just blinky-watch \
  & just blinky-bm-watch \
  & just motor-watch \
  & just motor-bm-watch \
  & just pid-watch \
  & just pid-bm-watch \
  & wait

clean:
  rm -rf ./*/{.zig-cache,zig-out}

blinky-build: (_build "1-blinky")
blinky-watch: (_watch "1-blinky")

blinky-bm-build: (_build-bm "1" "blinky")
blinky-bm-watch: (_watch-bm "1" "blinky")
blinky-bm-flash: (_cmd-bm "1" "blinky" "flash" "monitor")
blinky-bm-cmd *CMD: (_cmd-bm "1" "blinky" CMD)

motor-build: (_build "2-motor")
motor-watch: (_watch "2-motor")

motor-bm-build: (_build-bm "2" "motor")
motor-bm-watch: (_watch-bm "2" "motor")
motor-bm-flash: (_cmd-bm "2" "motor" "flash" "monitor")
motor-bm-cmd *CMD: (_cmd-bm "2" "motor" CMD)

pid-build: (_build "3-pid")
pid-watch: (_watch "3-pid")

pid-bm-build: (_build-bm "3" "pid")
pid-bm-watch: (_watch-bm "3" "pid")
pid-bm-flash: (_cmd-bm "3" "pid" "flash" "monitor")
pid-bm-cmd *CMD: (_cmd-bm "3" "pid" CMD)

_watch PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{PROJECT}} \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build \"{{PROJECT}}\"

_build PROJECT:
  cd {{PROJECT}} \
  && zvm run 0.13.0 build \
    -Dtarget={{arch}} \
    -Dcpu={{cpu}} \
    {{ if release != "" {"--release="+release} else {""} }} \
    -freference-trace \
  && cp ./zig-out/bin/* ../../artifacts/

_watch-bm NUM PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{NUM}}-{{PROJECT}}-bm \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build-bm \"{{NUM}}\" \"{{PROJECT}}\"

_build-bm NUM PROJECT: (_cmd-bm NUM PROJECT "build")
  cp {{NUM}}-{{PROJECT}}-bm/build/{{PROJECT}}.bin ../artifacts/{{NUM}}-{{PROJECT}}-bm-zig

_cmd-bm NUM PROJECT *CMD:
  cd {{NUM}}-{{PROJECT}}-bm && idf.py {{CMD}}
