artifacts_dir := "../artifacts/"

build-all: helloworld-build motorcontroller-build

watch-all:
  just helloworld-watch \
  & just motorcontroller-watch \
  & wait

clean:
  rm -rf ./*/{.zig-cache,zig-out}

helloworld-build: (_build "1-hello-world")
helloworld-watch: (_watch "1-hello-world")

motorcontroller-build: (_build "2-motor-controller")
motorcontroller-watch: (_watch "2-motor-controller")

_watch PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{PROJECT}} --on-busy-update=restart \
  -- just _build \"{{PROJECT}}\"

_build PROJECT:
  cd {{PROJECT}} \
  && zig build -Drpi --release \
  && cp ./zig-out/bin/* ../../artifacts/

