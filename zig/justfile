release := ""
#release := "fast"

arch := "arm-linux-gnueabihf"
cpu := "arm1176jzf_s"
artifacts_dir := "../artifacts/"

build-all: blinky-build motorcontroller-build pid-build

watch-all:
  just blinky-watch \
  & just motorcontroller-watch \
  & just pid-watch \
  & wait

clean:
  rm -rf ./*/{.zig-cache,zig-out}

blinky-build: (_build "1-blinky")
blinky-watch: (_watch "1-blinky")

motorcontroller-build: (_build "2-motor-controller")
motorcontroller-watch: (_watch "2-motor-controller")

pid-build: (_build "3-pid")
pid-watch: (_watch "3-pid")

_watch PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{PROJECT}} --on-busy-update=restart \
  -- just _build \"{{PROJECT}}\"

_build PROJECT:
  cd {{PROJECT}} \
  && zig build \
    -Dtarget={{arch}} \
    -Dcpu={{cpu}} \
    {{ if release != "" {"--release="+release} else {""} }} \
    -freference-trace \
  && cp ./zig-out/bin/* ../../artifacts/

