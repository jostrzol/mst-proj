release := if env("BUILD_PROFILE", "") == "SMALL" {
  "small"
} else if env("BUILD_PROFILE", "") == "FAST" {
  "fast"
} else {
  ""
}
build_flags := if release != "" {"--release="+release} else {""}
export ZIG_BUILD_ARGS := build_flags

arch := "arm-linux-gnueabihf"
cpu := "arm1176jzf_s"
artifacts_dir := "../artifacts" / lowercase(env("BUILD_PROFILE", "DEBUG"))
esp_export := ". ../esp-export/zig.sh"

build: build-os build-bm
build-os: blinky-build motor-build pid-build
build-bm:
  if test -z "${IDF_PATH:-}"; then {{esp_export}}; fi; \
  just blinky-bm-build && \
  just motor-bm-build && \
  just pid-bm-build

watch:
  just watch-os \
  & just watch-bm \
  & wait

watch-os:
  just blinky-watch \
  & just motor-watch \
  & just pid-watch \
  & wait

watch-bm:
  if test -z "${IDF_PATH:-}"; then {{esp_export}}; fi; \
  just blinky-bm-watch \
  & just motor-bm-watch \
  & just pid-bm-watch \
  & wait

clean:
  rm -rf ./*/{.zig-cache,zig-out}

blinky-build: (_build "1-blinky")
blinky-watch: (_watch "1-blinky")

blinky-bm-build: (_build-bm "1" "blinky")
blinky-bm-watch: (_watch-bm "1" "blinky")
blinky-bm-flash: (_cmd-bm "1" "blinky" "flash" "monitor")
blinky-bm-cmd *CMD: (_cmd-bm "1" "blinky" CMD)

motor-build: (_build "2-motor")
motor-watch: (_watch "2-motor")

motor-bm-build: (_build-bm "2" "motor")
motor-bm-watch: (_watch-bm "2" "motor")
motor-bm-flash: (_cmd-bm "2" "motor" "flash" "monitor")
motor-bm-cmd *CMD: (_cmd-bm "2" "motor" CMD)

pid-build: (_build "3-pid")
pid-watch: (_watch "3-pid")

pid-bm-build: (_build-bm "3" "pid")
pid-bm-watch: (_watch-bm "3" "pid")
pid-bm-flash: (_cmd-bm "3" "pid" "flash" "monitor")
pid-bm-cmd *CMD: (_cmd-bm "3" "pid" CMD)

_watch PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{PROJECT}} \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build \"{{PROJECT}}\"

_build PROJECT: _make_artifact_dir
  cd {{PROJECT}} \
  && zvm run 0.13.0 build \
    -Dtarget={{arch}} \
    -Dcpu={{cpu}} \
    {{build_flags}} \
    -freference-trace \
  && cp ./zig-out/bin/* ../{{artifacts_dir}}

_watch-bm NUM PROJECT:
  if test -z "${IDF_PATH:-}"; then {{esp_export}}; fi; \
  watchexec --postpone --wrap-process session --verbose \
  -e zig -w ./{{NUM}}-{{PROJECT}}-bm \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build-bm \"{{NUM}}\" \"{{PROJECT}}\"

_build-bm NUM PROJECT: (_cmd-bm NUM PROJECT "build") _make_artifact_dir
  cp {{NUM}}-{{PROJECT}}-bm/build/{{PROJECT}}.bin {{artifacts_dir}}/{{NUM}}-{{PROJECT}}-bm-zig

_cmd-bm NUM PROJECT *CMD:
  if test -z "${IDF_PATH:-}"; then {{esp_export}}; fi; \
  cd {{NUM}}-{{PROJECT}}-bm && idf.py -DSDKCONFIG="$SDKCONFIG" {{CMD}}

_make_artifact_dir:
  mkdir -p {{artifacts_dir}}
