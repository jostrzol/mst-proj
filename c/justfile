artifacts_dir := "../artifacts"

build-all: blinky-build blinky-bm-build motor-build motor-bm-build pid-build pid-bm-build

watch-all:
  just blinky-watch \
  & just blinky-bm-watch \
  & just motor-watch \
  & just motor-bm-watch \
  & just pid-watch \
  & just pid-bm-watch \
  & wait

clean:
  rm -rf ./build

blinky-build: (_build "1-blinky")
blinky-watch: (_watch "1-blinky")

blinky-bm-build: (_build-bm "1-blinky-bm")
blinky-bm-watch: (_watch-bm "1-blinky-bm")
blinky-bm-flash: (_cmd-bm "1-blinky-bm" "flash" "monitor")
blinky-bm-cmd *CMD: (_cmd-bm "1-blinky-bm" CMD)

motor-build: (_build "2-motor")
motor-watch: (_watch "2-motor")

motor-bm-build: (_build-bm "2-motor-bm")
motor-bm-watch: (_watch-bm "2-motor-bm")
motor-bm-flash: (_cmd-bm "2-motor-bm" "flash" "monitor")
motor-bm-cmd *CMD: (_cmd-bm "2-motor-bm" CMD)

pid-build: (_build "3-pid")
pid-watch: (_watch "3-pid")

pid-bm-build: (_build-bm "3-pid-bm")
pid-bm-watch: (_watch-bm "3-pid-bm")
pid-bm-flash: (_cmd-bm "3-pid-bm" "flash" "monitor")
pid-bm-cmd *CMD: (_cmd-bm "3-pid-bm" CMD)

_watch PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e c,h -w ./{{PROJECT}} \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build \"{{PROJECT}}\"

_build PROJECT:
  mkdir -p build && cd build \
  && cmake -Werror=dev --warn-uninitialized .. \
  && make {{PROJECT}} \
  && cp ./bin/{{PROJECT}} ../{{artifacts_dir}}/{{PROJECT}}-c

_watch-bm PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e c,h -w ./{{PROJECT}}/main \
  --on-busy-update=restart --stop-signal=SIGKILL \
  -- just _build-bm \"{{PROJECT}}\"

_build-bm PROJECT: (_cmd-bm PROJECT "build")
  cd {{PROJECT}} && idf.py build

_cmd-bm PROJECT *CMD:
  cd {{PROJECT}} && idf.py {{CMD}}
