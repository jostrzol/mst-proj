artifacts_dir := "../artifacts"

build-all: blinky-build blinky-bm-build motorcontroller-build pid-build

watch-all:
  just blinky-watch \
  & just blinky-bm-watch \
  & just motorcontroller-watch \
  & just pid-watch \
  & wait

clean:
  rm -rf ./build

blinky-build: (_build "1-blinky")
blinky-watch: (_watch "1-blinky")

blinky-bm-build: (_build "1-blinky-bm")
blinky-bm-watch: (_watch "1-blinky-bm")

motorcontroller-build: (_build "2-motor-controller")
motorcontroller-watch: (_watch "2-motor-controller")

pid-build: (_build "3-pid")
pid-watch: (_watch "3-pid")

_watch PROJECT:
  watchexec --postpone --wrap-process session --verbose \
  -e c,h -w ./{{PROJECT}} --on-busy-update=restart \
  -- just _build \"{{PROJECT}}\"

_build PROJECT:
  mkdir -p build && cd build && cmake -Werror=dev --warn-uninitialized .. && make {{PROJECT}} \
  && cp ./bin/{{PROJECT}} ../{{artifacts_dir}}/{{PROJECT}}-c

